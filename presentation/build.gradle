plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'de.mannodermaus.android-junit5'
    id 'com.google.android.gms.oss-licenses-plugin'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def localPropertiesFile = rootProject.file("local.properties")
def properties = new Properties()
properties.load(new FileInputStream(localPropertiesFile))

android {
    namespace 'com.ccc.remind'
    compileSdk 34

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.ccc.remind"
        minSdk 27
        targetSdkVersion 34
        versionCode 5
        versionName "0.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        defaultConfig {
            buildConfigField(
                    "String",
                    "KAKAO_NATIVE_APP_KEY",
                    properties.getProperty("KAKAO_NATIVE_APP_KEY")
            )
            buildConfigField(
                    "String",
                    "SOCKET_URL",
                    properties.getProperty("SOCKET_URL")
            )

            manifestPlaceholders["NATIVE_APP_KEY"] =
                    properties.getProperty("KAKAO_NATIVE_APP_KEY_NO_QUOTES")

            manifestPlaceholders["SERVER_HOST"] =
                    properties.getProperty("RELEASE_SERVER_HOST_NO_QUOTES")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField(
                    "String",
                    "SERVER_URL",
                    properties.getProperty("RELEASE_SERVER_URL")
            )
        }

        debug {
            signingConfig signingConfigs.release
            buildConfigField(
                    "String",
                    "SERVER_URL",
                    properties.getProperty("DEBUG_SERVER_URL")
            )
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    buildFeatures {
        dataBinding true
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.8"
    }
}

dependencies {
    implementation project(':data')
    implementation project(':domain')

    implementation libs.androidx.core.ktx
    implementation libs.androidx.core.splashscreen
    implementation libs.kotlin.reflect
    implementation libs.androidx.appcompat
    implementation libs.google.material
    implementation libs.androidx.constraint.layout
    implementation libs.bundles.androidx.activity
    implementation libs.bundles.androidx.fragment
    implementation platform(libs.compose.bom)
    implementation libs.bundles.compose
    debugImplementation libs.bundles.compose.debug
    androidTestImplementation libs.bundles.compose.test
    implementation libs.bundles.androidx.lifecycle
    implementation platform(libs.firebase.bom)
    implementation libs.bundles.firebase
    implementation libs.work.runtime
    implementation libs.kakao.sdk.v2.user
    implementation libs.kakao.sdk.v2.all
    implementation libs.bundles.hilt
    kapt libs.hilt.compiler
    implementation libs.room.runtime
    implementation libs.bundles.coil
    implementation libs.timber
    implementation libs.circleindicator
    implementation libs.bundles.androidx.navigation
    implementation libs.sandwich
    implementation libs.androidbrowserhelper
    implementation libs.accompanist.permissions
    implementation libs.play.services.oss.licenses
    implementation libs.calendar.compose

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso.core
    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine
    testImplementation libs.truth
    androidTestImplementation libs.truth
}

kapt {
    correctErrorTypes true
}